# Maintainer: Skirmantas Kligys ("wookietreiber") <Skirmantas.Kligys@gmail.com>
# Modified from: https://aur.archlinux.org/packages/dfhack/
pkgname=dfhack-twbt-sk
pkgver=0.47.04
_pkgver=$pkgver-r1
pkgrel=3
pkgdesc="memory hacking library for Dwarf Fortress and a set of tools that use it"
arch=('x86_64' 'i686')
url="https://dfhack.readthedocs.io/en/stable/"
license=('custom')
depends=("dwarffortress-sk=$pkgver" lua protobuf libpng12 libxrandr libjpeg6 freetype2 libglvnd libxcursor libxinerama)
makedepends=('cmake' 'git' 'python-sphinx' 'perl-xml-libxml' 'perl-xml-libxslt')
conflicts=('dfhack-bin' 'dfhack-git' 'dfhack-twbt-bin' 'dfhack' 'dfhack-twbt')

source=(dfhack::git+https://github.com/skligys/dfhack#tag="$_pkgver"
        twbt::git+https://github.com/skligys/df-twbt#tag="$_pkgver"
        dfhack.sh
        dfhack-run.sh)

md5sums=('SKIP'
         'SKIP'
         '81f5909c1a32391679f968e40f24d5ca'
         '3853c6f890d3541f710f2c4833a9e696')

prepare() {
  cd "$srcdir"/dfhack || exit 1

  git remote set-url origin https://github.com/skligys/dfhack
  git submodule sync
  git submodule update --init
}

build() {
  cd "$srcdir"/dfhack/build || exit 1

  cmake \
    -DCMAKE_INSTALL_PREFIX=/opt/dwarffortress \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_DOCS=ON \
    -DBUILD_STONESENSE=ON \
    -DDFHACK_BUILD_ARCH=64 \
    ..

  make

  cd "$srcdir"/twbt || exit 1
  DH="$srcdir"/dfhack make
}

package() {
  cd "$srcdir"/dfhack/build || exit 1

  # shellcheck disable=2154
  make DESTDIR="$pkgdir" install

  install -Dm755 "$srcdir"/dfhack.sh     "$pkgdir"/usr/bin/dfhack
  install -Dm755 "$srcdir"/dfhack-run.sh "$pkgdir"/usr/bin/dfhack-run

  install -Dm644 ../LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  cd "$srcdir"/twbt || exit 1
  DF=$pkgdir/opt/dwarffortress make inst
}
